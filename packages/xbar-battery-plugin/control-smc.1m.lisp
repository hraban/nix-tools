#!/usr/bin/env sbcl --script
;; <xbar.title>Mac Battery Control</xbar.title>
;; <xbar.author>Hraban Luyat</xbar.author>
;; <xbar.author.github>hraban</xbar.author.github>
;; <xbar.desc>Control your Mac laptop‚Äôs maximum battery charge to improve its longevity</xbar.desc>
;; <xbar.var>boolean(DEBUG=false): Verbose output</xbar.var>
;; <xbar.dependencies>lisp</xbar.dependencies>

;; Copyright ¬© 2024  Hraban Luyat
;;
;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU Affero General Public License as published
;; by the Free Software Foundation, version 3 of the License.
;;
;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU Affero General Public License for more details.
;;
;; You should have received a copy of the GNU Affero General Public License
;; along with this program.  If not, see <https://www.gnu.org/licenses/>.

(setf *compile-verbose* NIL)
(setf *trace-output* *error-output*)

(require "asdf")
(require "uiop")

(asdf:load-system "cl-interpol")
(asdf:load-system "cl-ppcre")
(asdf:load-system "inferior-shell")
(asdf:load-system "trivia")

(defpackage #:battery
  (:use #:cl)
  (:local-nicknames (#:sh #:inferior-shell)))

(in-package #:battery)

(named-readtables:in-readtable :interpol-syntax)

(defvar *smc_off* "@smc_off@")
(defvar *smc_on* "@smc_on@")
(defvar *smc* "@smc@")
(defvar *me* "@self@")

(defun debugp ()
  (string= "true" (uiop:getenv "DEBUG")))

(defun sh (&rest args)
  (apply #'sh:run `(,@args :show ,(debugp))))

(defun sh/ss (&rest args)
  (apply #'sh `(,@args :output (:string :stripped t))))

(defun smc-read ()
  (sh/ss `(,*smc* #\k "CHTE" #\r)))

(defun charging-state ()
  (or
   (cl-ppcre:register-groups-bind (n) ("\\(bytes ([\\d ]+)\\)" (smc-read))
                                  (trivia:match n
                                    ("00 00 00 00" "‚ö°")
                                    ("01 00 00 00" "üîå")))
   "‚ùì"))

(defun println (s)
  (format T "~A~%" s))

(defun boolstr (b)
  (if b "true" "false"))

(defun print-menu ()
  (println (charging-state))
  (println "---")
  (println #?"‚ö° | shell=${*me*} | param1=on | terminal=${(boolstr (debugp))} | refresh=true")
  (println #?"üîå | shell=${*me*} | param1=off | terminal=${(boolstr (debugp))} | refresh=true"))

(defun main ()
  (trivia:match (uiop:command-line-arguments)
    (()
     (print-menu))
    ((list "on")
     (sh `(sudo ,*smc_on*))
     (print-menu))
    ((list "off")
     (sh `(sudo ,*smc_off*))
     (print-menu))))

(main)
